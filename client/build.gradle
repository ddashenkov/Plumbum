buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.2'
    }
}

apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'maven-publish'

dependencies {
    compile project(path: ':model')
}

shadowJar {
    baseName = 'plumbum-client'
    classifier = null
    version = null

    configurations = [project.configurations.compile]

    append 'known_types.properties'

    final Set<String> fileNames = new HashSet<>()
    exclude { descriptor ->
        final def name = descriptor.name
        if (!descriptor.isDirectory() && fileNames.contains(name) && name.contains("/")) {
            return true
        } else {
            fileNames.add(name)
            return false
        }
    }
    final def blacklist = ['google.protobuf', 'com.google.protobuf']
    exclude { descriptor ->
        final def className = descriptor.name.replace('/', '.')
        for (def pattern in blacklist) {
            if (className.startsWith(pattern)) return true
        }
        return false
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId 'edu.ddashenkov.plumbum'
            artifactId 'client'
            version '0.1'

            artifact shadowJar
        }
    }
}

